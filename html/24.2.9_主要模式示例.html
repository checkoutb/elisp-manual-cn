<!DOCTYPE html><html><head><link rel="stylesheet" type="text/css" href="elisp.css"><title>24.2.9 主要模式示例</title></head><body lang="en" bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#800080" alink="#FF0000"><h3 class="subsection">24.2.9 主要模式示例</h3>
<div class="nav-panel"><p>Next: <a href="24.3_次要模式.html">次要模式</a>, Previous: <a href="24.2.8_通用模式.html">通用模式</a>, Up: <a href="24.2_主要模式.html">主要模式.</a> &nbsp; [<a href="00_content.htm" title="Table of contents" rel="contents">Contents</a>]</p></div></body></html><hr/><p>文本模式可能是除基本模式之外最简单的模式。以下是 text-mode.el 的摘录，说明了上面列出的许多约定：
</p>
<div class="lisp">
<pre class="lisp">

;; Create the syntax table for this mode.
(defvar text-mode-syntax-table
  (let ((st (make-syntax-table)))
    (modify-syntax-entry ?\" ".   " st)
    (modify-syntax-entry ?\\ ".   " st)
    ;; Add 'p' so M-c on 'hello' leads to 'Hello', not 'hello'.
    (modify-syntax-entry ?' "w p" st)
    …
    st)
  "Syntax table used while in `text-mode'.")


;; Create the keymap for this mode.

(defvar text-mode-map
  (let ((map (make-sparse-keymap)))
    (define-key map "\e\t" 'ispell-complete-word)
    …
    map)
  "Keymap for `text-mode'.
Many other modes, such as `mail-mode', `outline-mode' and
`indented-text-mode', inherit all the commands defined in this map.")
</pre></div>
<p>以下是 Lisp 模式语法和缩写表的定义方式：
</p><div class="lisp">
<pre class="lisp">(define-derived-mode text-mode nil "Text"
  "Major mode for editing text written for humans to read.
In this mode, paragraphs are delimited only by blank or white lines.
You can thus get the full benefit of adaptive filling
 (see the variable `adaptive-fill-mode').
\\{text-mode-map}
Turning on Text mode runs the normal hook `text-mode-hook'."

  (setq-local text-mode-variant t)
  (setq-local require-final-newline mode-require-final-newline))
</pre></div>
<p>Lisp 的三种模式共享它们的大部分代码。例如，Lisp 模式和 Emacs Lisp 模式继承自 Lisp Data 模式，Lisp 交互模式继承自 Emacs Lisp 模式。
</p>
<p>除此之外，Lisp 数据模式设置了 comment-start 变量来处理 Lisp 注释：
</p>
<div class="lisp">
<pre class="lisp">(setq-local comment-start ";")
…
</pre></div>
<p>每种不同的 Lisp 模式都有一个稍微不同的键盘映射。例如，Lisp 模式将 Cc Cz 绑定到 run-lisp，但其他 Lisp 模式没有。然而，所有的 Lisp 模式都有一些共同的命令。以下代码设置常用命令：
</p><div class="lisp">
<pre class="lisp">(defvar lisp-mode-shared-map
  (let ((map (make-sparse-keymap)))
    (set-keymap-parent map prog-mode-map)
    (define-key map "\e\C-q" 'indent-sexp)
    (define-key map "\177" 'backward-delete-char-untabify)
    map)
  "Keymap for commands shared by all sorts of Lisp modes.")
</pre></div>
<p>这是为 Lisp 模式设置键盘映射的代码：
</p><div class="lisp">
<pre class="lisp">(defvar lisp-mode-map
  (let ((map (make-sparse-keymap))
        (menu-map (make-sparse-keymap "Lisp")))
    (set-keymap-parent map lisp-mode-shared-map)
    (define-key map "\e\C-x" 'lisp-eval-defun)
    (define-key map "\C-c\C-z" 'run-lisp)
    …
    map)
  "Keymap for ordinary Lisp mode.
All commands in `lisp-mode-shared-map' are inherited by this map.")
</pre></div>
<p>最后，这是 Lisp 模式的主要模式命令：
</p><div class="lisp">
<pre class="lisp">(define-derived-mode lisp-mode lisp-data-mode "Lisp"
  "Major mode for editing Lisp code for Lisps other than GNU Emacs Lisp.
Commands:
Delete converts tabs to spaces as it moves back.
Blank lines separate paragraphs.  Semicolons start comments.

\\{lisp-mode-map}
Note that `run-lisp' may be used either to start an inferior Lisp job
or to switch back to an existing one."

  (setq-local find-tag-default-function 'lisp-find-tag-default)
  (setq-local comment-start-skip
              "\\(\\(^\\|[^\\\n]\\)\\(\\\\\\\\\\)*\\)\\(;+\\|#|\\) *")
  (setq imenu-case-fold-search t))
</pre></div>
<hr/>

<div class="nav-panel"><p>Next: <a href="24.3_次要模式.html">次要模式</a>, Previous: <a href="24.2.8_通用模式.html">通用模式</a>, Up: <a href="24.2_主要模式.html">主要模式.</a> &nbsp; [<a href="00_content.htm" title="Table of contents" rel="contents">Contents</a>]</p></div></body></html>